#!/usr/bin/env raku

use v6;
use lib 'lib';

=begin pod

=head1 NAME

Clu - blah blah blah

=head1 SYNOPSIS

=begin code :lang<raku>

foo

=end code

=head1 DESCRIPTION

Clu is ...

=head1 AUTHOR

masukomi <masukomi@masukomi.org>

=head1 COPYRIGHT AND LICENSE

Copyright 2022 masukomi

This library is free software; you can redistribute it and/or modify it under the MIT License;

=end pod



# PROBABLE DEPENDENCIES
# Terminal::ANSIColor
# Text::MiscUtils
# Text::MiscUtils::Layout ( tables, columns, etc )
# TOML
# DB::SQLite (search)
# Color
# Test::Output
#

use DB::SQLite;

use Clu::Resourcer;
use Clu::Command;
use Clu::Ingester;

my $*MAIN-ALLOW-NAMED-ANYWHERE = True;

multi sub MAIN('add', Str $path) {ingest($path);}

multi sub MAIN ('find', *@search_strings){
	guarantee-db();
	die("Search strings(s) must be provided") unless @search_strings.elems > 0;
	my $db = DB::SQLite.new(filename => "$*HOME/.config/clu/database.db", :readonly);
	my $search_string = @search_strings.join(' ');
	search-and-display($search_string, $db);

	$db.finish();

}

multi sub MAIN ('list') {
	guarantee-db();
	my $db = DB::SQLite.new(filename => "$*HOME/.config/clu/database.db", :readonly);
	list-all-commands($db);
	$db.finish();
}

multi sub MAIN ('remove', $command_name) {
	guarantee-db();
	my $db = DB::SQLite.new(filename => "$*HOME/.config/clu/database.db");
	remove-command($command_name, $db)
		?? say("$command_name has been removed")
		!! say("no command found with name $command_name");

	$db.finish();
}


# #= display full details on a command
multi sub MAIN ('show', $command_name) {
	guarantee-db();
	my $db = DB::SQLite.new(filename => "$*HOME/.config/clu/database.db", :readonly);
	display-command($command_name, $db);

	$db.finish();
}

multi sub MAIN('template', $destination) {
	say("Copying fresh template to $destination");
	copy(Clu::Resourcer.gimme{"template.meta.toml"}, $destination);
}


# #= display cheats for a command
# sub MAIN ('cheats', $command_name) {
#	my $db = DB::SQLite.new(filename => "$*HOME/.config/clu/database.db", :readonly)
# 	Clu::Cheats.new($command_name)
# 	# TODO: support interacting with the cheats
# 	# to choose one.
# 	# Then leveraging them as templates with params
# 	# that the user can fill in and have it executed.
# }

multi sub MAIN('update', Str $path) {ingest($path);}


#---------------
#internal stuff

sub ingest(Str $path) {
	guarantee-db();
	my $db = DB::SQLite.new(filename => "$*HOME/.config/clu/database.db");
	my $response = ingest-metadata($path, $db);
	$db.finish;
	if $response {
		say("successfully ingested $path");
		exit 0;
    } else {
	    warn("problems ingesting $path");
		exit 1;
	}

}


sub copy-over-db($destination) {
	say("Copying fresh db to $destination");
	copy(Clu::Resourcer.gimme{"empty_database.db"}, $destination);
}

sub guarantee-db() returns Bool {
	my $dir = "$*HOME/.config/clu";
	my $path = "$dir/database.db";
	return True if $path.IO.e;
	unless $dir.IO.e {
		say("Creating $dir");
		mkdir($dir);
	}

	unless $path.IO.e {
		copy-over-db($path);
	}

	CATCH {
		when X::IO::Mkdir { die "Unable to create directory: $dir"; }
		when X::IO::Copy  { die "Unable to copy resource to $dir"; }
	}
}
